@_default:
    just --list

build:
    #!/usr/bin/env bash

    if [ ! -f logo.png ]; then \
        just logo; \
    fi

    nativefier \
        --darwin-dark-mode-support \
        --icon logo.png \
        --lang "en-US" \
        --name "ChatGPT" \
        --quiet \
        https://chat.openai.com/chat \
        build

    rsync \
        --archive \
        --delete-after \
        --quiet \
        build/ChatGPT-darwin-*/ChatGPT.app/ \
        ~/Applications/ChatGPT.app

@clean:
    rm -rf build

@fmt:
    just --fmt --unstable

@logo:
    wget \
        --quiet \
        --output-document=logo.png \
        https://logo.clearbit.com/chat.openai.com

@upgrade:
    nativefier \
        --quiet \
        --upgrade \
        build/ChatGPT-darwin-*/ChatGPT.app/

    rsync \
        --archive \
        --delete-after \
        --quiet \
        build/ChatGPT-darwin-*/ChatGPT.app/ \
        ~/Applications/ChatGPT.app
